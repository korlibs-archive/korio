import org.apache.tools.ant.taskdefs.condition.Os

buildscript {
	ext.kotlinVersion = '1.2.0-beta-88'
	ext.compilerVersion = ext.kotlinVersion
	ext.librariesVersion = ext.kotlinVersion
	ext.eapChannel = 'http://dl.bintray.com/kotlin/kotlin-eap-1.2'
	ext.serializationPluginVersion = '0.1'

	repositories {
		jcenter()
		maven { url eapChannel }
		maven { url "https://plugins.gradle.org/m2/" }
		mavenLocal()
	}

	dependencies {
		classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$compilerVersion"
	}
}

allprojects {
	repositories {
		mavenLocal()
		jcenter()
		maven { url eapChannel }
		mavenLocal()
	}

	group 'com.soywiz'
	version "$korVersion"

	apply plugin: 'java'
	apply plugin: 'maven'
	apply plugin: 'signing'
	apply plugin: 'maven-publish'
	apply plugin: 'idea'

	it.afterEvaluate {
		if (it.plugins.hasPlugin("kotlin-platform-common")) {
			dependencies {
				compile "org.jetbrains.kotlin:kotlin-stdlib-common:$librariesVersion"
				testCompile "org.jetbrains.kotlin:kotlin-test-common:$librariesVersion"
				testCompile "org.jetbrains.kotlin:kotlin-test-annotations-common:$librariesVersion"
			}

			kotlin {
				experimental { coroutines 'enable' }
			}
		}
		if (it.plugins.hasPlugin("kotlin-platform-jvm") || it.plugins.hasPlugin("kotlin")) {
			dependencies {
				compile "org.jetbrains.kotlin:kotlin-stdlib:$librariesVersion"
				testCompile "org.jetbrains.kotlin:kotlin-test:$librariesVersion"
				testCompile "org.jetbrains.kotlin:kotlin-test-junit:$librariesVersion"
				testCompile "junit:junit:4.12"
			}

			kotlin {
				experimental { coroutines 'enable' }
			}

			compileJava.options.encoding = 'UTF-8'
			compileTestJava.options.encoding = 'UTF-8'

			sourceCompatibility = 1.7
			targetCompatibility = 1.7
		}

		// https://discuss.kotlinlang.org/t/unit-testing-in-kotlin-js/3943
		// https://github.com/JetBrains/kotlin-examples/blob/5e883a6d67afc8b8aeb8991af6a7b6183be2213f/gradle/js-tests/mocha/build.gradle
		if (it.plugins.hasPlugin("kotlin-platform-js") || it.plugins.hasPlugin("kotlin2js")) {
			dependencies {
				compile "org.jetbrains.kotlin:kotlin-stdlib-js:$librariesVersion"
				testCompile "org.jetbrains.kotlin:kotlin-test-js:$librariesVersion"
			}

			kotlin {
				experimental { coroutines 'enable' }
			}

			[compileKotlin2Js, compileTestKotlin2Js]*.configure {
				kotlinOptions.moduleKind = "umd"
				kotlinOptions.sourceMap = true
			}

			task populateNodeModules(type: Copy, dependsOn: compileKotlin2Js) {
				from compileKotlin2Js.destinationDir

				configurations.testCompile.each {
					from zipTree(it.absolutePath).matching { include '*.js' }
				}

				into "${buildDir}/node_modules"
			}

			//node {
			//	download = true
			//}

			//task installMocha(type: NpmTask) {
			//	workingDir = "${buildDir}"
			//	args = ['install', 'mocha']
			//}

			task installMocha(type: Exec) {
				workingDir "$buildDir"
				if (Os.isFamily(Os.FAMILY_WINDOWS)) {
					commandLine 'cmd', '/c', 'npm.cmd', 'install', 'mocha'
				} else {
					commandLine 'npm', 'install', 'mocha'
				}
			}

			//task fixJsForAsync(type: Task, dependsOn: [compileTestKotlin2Js, populateNodeModules, installMocha]) {
			//	doLast {
			//		File file = new File(compileTestKotlin2Js.outputFile)
			//		File fileOut = new File(compileTestKotlin2Js.outputFile + ".fix.js")
			//		//file.text = 'function test2(name, v, func) { test(name, v, () => { var res = func(); return (res instanceof Promise) ? res : undefined; }); }; ' + file.text.replaceAll(/test\('(.*)',\s*false,\s*function\s*\(\)\s* \{/, 'test2("$1", false, function() { return')
			//		fileOut.text = file.text.replaceAll(
			//				/(?m)(?s)test\('(.*?)', false, function \(\) \{\s*(.*?);\s*\}\);/,
			//				'test("$1", false, function() { var res = $2; return (res instanceof Promise) ? res : undefined; });'
			//		)
			//	}
			//}

			task fixJsForAsync(type: Task, dependsOn: [compileTestKotlin2Js, populateNodeModules, installMocha]) {
				doLast {
					File file = new File(compileTestKotlin2Js.outputFile)
					if (file.exists()) {
						File fileOut = new File(compileTestKotlin2Js.outputFile + ".fix.js")
						//file.text = 'function test2(name, v, func) { test(name, v, () => { var res = func(); return (res instanceof Promise) ? res : undefined; }); }; ' + file.text.replaceAll(/test\('(.*)',\s*false,\s*function\s*\(\)\s* \{/, 'test2("$1", false, function() { return')

						def timeout = 2000
						//def timeout = 5000

						fileOut.text = file.text.replaceAll(
								/(?m)(?s)test\('(.*?)', false, function \(\) \{\s*(.*?);\s*\}\);/,
								'test("$1", false, function() { this.timeout(' + timeout + '); global.testPromise = null; var res = $2 || (global.testPromise); return (res instanceof Promise) ? res : undefined; });'
						)
					}
				}
			}

			task runMocha(type: Task, dependsOn: [compileTestKotlin2Js, populateNodeModules, installMocha, fixJsForAsync]) {
				doLast {
					File fileOut = new File(compileTestKotlin2Js.outputFile + ".fix.js")

					if (fileOut.exists()) {
						String[] cmd
						if (Os.isFamily(Os.FAMILY_WINDOWS)) {
							cmd = ["cmd", "/c", "$buildDir/node_modules/.bin/mocha.cmd" as String, fileOut]
						} else {
							cmd = ["$buildDir/node_modules/.bin/mocha" as String, fileOut]
						}

						ProcessBuilder pb = new ProcessBuilder(cmd as String[])
						pb.directory(new File("$buildDir/node_modules"))
						def p = pb.start()
						p.in.eachLine { println(it) }
						if (p.waitFor() != 0) {
							throw new GradleException('error occurred')
						}
					}
				}
			}

			test.dependsOn runMocha
		}
	}

	//kotlin { experimental { coroutines 'enable' } }

	task javadoc2(type: Javadoc) {
		failOnError = false
	}

	task javadocJar(type: Jar, dependsOn: javadoc2) {
		classifier = 'javadoc'
		from 'build/docs/javadoc'
	}

	task sourcesJar(type: Jar) {
		from sourceSets.main.allSource
		classifier = 'sources'
	}

	artifacts {
		//archives jar
		archives javadocJar
		archives sourcesJar
	}

// gradle uploadArchives
	if (project.hasProperty('sonatypeUsername')) {
		signing {
			sign configurations.archives
		}

		uploadArchives {
			repositories {
				mavenDeployer {
					beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

					repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
						authentication(userName: project.sonatypeUsername, password: project.sonatypePassword)
					}

					pom.project {
						name "${project.name}"
						packaging 'jar'
						description 'Korio: Kotlin cORoutines I/O : Streams + Async TCP Client/Server + Virtual File System for JVM, Node.JS and Browser'
						url 'https://github.com/soywiz/korio/'
						inceptionYear '2016'

						scm {
							url 'scm:git@github.com:soywiz/korio.git'
							connection 'scm:git@github.com:soywiz/korio.git'
							developerConnection 'scm:git@github.com:soywiz/korio.git'
						}

						licenses {
							license {
								name 'The Apache Software License, Version 2.0'
								url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
								distribution 'repo'
							}
						}

						developers {
							developer {
								id 'soywiz'
								name 'Carlos Ballesteros Velasco'
							}
						}
					}
				}
			}
		}
	}

	publishing {
		publications {
			MyPublication(MavenPublication) {
				from components.java
				groupId project.group
				artifactId project.name
				version "$project.version"
			}
		}
	}

	task deploy(dependsOn: ['install', 'uploadArchives']) {
	}


}

