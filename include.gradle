group 'com.soywiz'
version "$korVersion"

if (project.hasProperty('useAndroid')) {
	project.apply plugin: 'com.android.library'
	apply plugin: 'kotlin-android'
} else {
	project.apply plugin: 'java'
	apply plugin: 'kotlin'
	compileJava.options.encoding = 'UTF-8'
	compileTestJava.options.encoding = 'UTF-8'
}
apply plugin: 'maven'
apply plugin: 'signing'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

sourceCompatibility = 1.7
targetCompatibility = 1.7

kotlin { experimental { coroutines 'enable' } }

configurations {
	provided
}

sourceSets {
	main.java.srcDirs = ['src']
	test.java.srcDirs = ['test']
	main.resources.srcDirs = ['resources']
	test.resources.srcDirs = ['testresources']
	main { compileClasspath += configurations.provided }
}

dependencies {
	compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"
	compile "com.jtransc:jtransc-rt-core-kotlin:$jtranscVersion"
	//compile "com.jtransc:jtransc-annotations:$jtranscVersion"
	testCompile "junit:junit:4.11"
}


task javadoc2(type: Javadoc) {
	failOnError = false
}

task javadocJar(type: Jar, dependsOn: javadoc2) {
	classifier = 'javadoc'
	from 'build/docs/javadoc'
}

task sourcesJar(type: Jar) {
	from sourceSets.main.allSource
	classifier = 'sources'
}

artifacts {
	//archives jar
	archives javadocJar
	archives sourcesJar
}

// gradle uploadArchives
if (project.hasProperty('sonatypeUsername')) {
	signing {
		sign configurations.archives
	}

	uploadArchives {
		repositories {
			mavenDeployer {
				beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

				repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
					authentication(userName: project.sonatypeUsername, password: project.sonatypePassword)
				}

				pom.project {
					name "${project.name}"
					packaging 'jar'
					description 'Korio: Kotlin cORoutines I/O : Streams + Async TCP Client/Server + Virtual File System for JVM, Node.JS and Browser'
					url 'https://github.com/soywiz/korio/'
					inceptionYear '2016'

					scm {
						url 'scm:git@github.com:soywiz/korio.git'
						connection 'scm:git@github.com:soywiz/korio.git'
						developerConnection 'scm:git@github.com:soywiz/korio.git'
					}

					licenses {
						license {
							name 'The Apache Software License, Version 2.0'
							url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
							distribution 'repo'
						}
					}

					developers {
						developer {
							id 'soywiz'
							name 'Carlos Ballesteros Velasco'
						}
					}
				}
			}
		}
	}
}

publishing {
	publications {
		MyPublication(MavenPublication) {
			from components.java
			groupId project.group
			artifactId project.name
			version "$project.version"
		}
	}
}

// gradle bintrayUpload
if (project.hasProperty('BINTRAY_USER')) {
	bintray {
		// https://github.com/bintray/gradle-bintray-plugin#buildgradle
		//Properties bintray = new Properties()
		//bintray.load(new FileInputStream(System.getProperty("user.home") + "/.bintray.properties"))

		user = BINTRAY_USER
		key = BINTRAY_KEY
		publications = ['MyPublication']
		publish = true
		override = true

		pkg {
			repo = "soywiz-maven"
			name = project.name
			userOrg = 'soywiz'
			licenses = ['Apache-2.0']
			vcsUrl = 'https://github.com/soywiz/korio.git'
		}
	}
}

task deploy(dependsOn: ['install', 'bintrayUpload', 'uploadArchives']) {
}